apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 28
    buildToolsVersion '29.0.2'

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    defaultConfig {
        applicationId "org.matrix.console"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 16
        versionName "0.5.4"
    }

    signingConfigs {
        release {
            storeFile file(project.hasProperty("RELEASE_STORE_FILE") ? RELEASE_STORE_FILE : "/dummy/path")
            storePassword project.hasProperty("RELEASE_STORE_PASSWORD") ? RELEASE_STORE_PASSWORD : "dummy password"
            keyAlias project.hasProperty("RELEASE_KEY_ALIAS") ? RELEASE_KEY_ALIAS : "dummy alias"
            keyPassword project.hasProperty("RELEASE_KEY_PASSWORD") ? RELEASE_KEY_PASSWORD : "dummy password"
        }
    }

    buildTypes {
        debug {
            resValue "string", "git_revision", "\"${gitRevision()}\""
            resValue "string", "git_revision_unix_date", "\"${gitRevisionUnixDate()}\""
            resValue "string", "git_revision_date", "\"${gitRevisionDate()}\""
        }

        release {
            resValue "string", "git_revision", "\"${gitRevision()}\""
            resValue "string", "git_revision_unix_date", "\"${gitRevisionUnixDate()}\""
            resValue "string", "git_revision_date", "\"${gitRevisionDate()}\""

            minifyEnabled false
            //shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        matrixorg.initWith(buildTypes.release)
        matrixorg {
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "geschmacksrichtung"

    productFlavors {
        gcm {
            applicationId "org.matrix.androidsdk.alpha"
            versionCode 16
            versionName "0.5.4"
        }

        foss {
            applicationId "org.matrix.androidsdk.alpha"
            versionCode 16
            versionName "0.5.4"
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    repositories {
        flatDir {
            dir 'libs'
        }
    }
}

static def gitRevision() {
    def cmd = "git rev-parse --short HEAD"
    return cmd.execute().text.trim()
}

static def gitRevisionUnixDate() {
    def cmd = "git show -s --format=%ct HEAD^{commit}"
    return cmd.execute().text.trim()
}

static def gitRevisionDate() {
    def cmd = "git show -s --format=%ci HEAD^{commit}"
    return cmd.execute().text.trim()
}

dependencies {
    implementation 'com.github.matrix-org:matrix-android-sdk2:v1.0.9'

    implementation 'me.leolin:ShortcutBadger:1.1.22@aar'

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.legacy:legacy-support-core-ui:1.0.0'
    gcmImplementation 'com.google.android.gms:play-services-gcm:17.0.0'
    gcmImplementation 'com.google.android.gms:play-services-analytics:17.0.0'

    implementation 'com.squareup.retrofit:retrofit:1.6.1'
    implementation 'com.google.code.gson:gson:2.3'
    implementation 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    implementation 'com.squareup.okhttp:okhttp:2.0.0'
    implementation 'org.sufficientlysecure:html-textview:1.4'
}

